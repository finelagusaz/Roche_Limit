///////// 文　ゴーストテンプレート
///////// 汎用アプリケーション/どりふ/きのこ辞書
///////// written by umeici.

//**** 汎用アプリケーション *************************************************************

//---- 起動 -----------------------------------------------------------------------------
OnApplicationBoot
{
	"\0\s[0]%reference[0]を起動します。\e"
}

//---- 終了 -----------------------------------------------------------------------------
OnApplicationClose
{
	"\0\s[0]%reference[0]を終了します。\e"
}

//---- ファイルオープン -----------------------------------------------------------------
OnApplicationFileOpen
{
	"\0\s[0]%reference[1]を開きました。\e"
}


//**** きのこ ***************************************************************************

//---- 生えた ---------------------------------------------------------------------------
OnKinokoObjectCreate
{
	"\0\s[0]何でこの身体に生えるんですか？\w9\n\n[half]/
	\1\s[10]考えたら負けだよ、\w4たぶん。\e"
}

//---- 消えた ---------------------------------------------------------------------------
OnKinokoObjectDestroy
{
	"\0\s[5]原状回復です。\e"
}

//---- バッテリーが低下 -----------------------------------------------------------------
OnBatteryLow
{
	"\1\s[10]バッテリーが無くなってきてるようだね。\0\s[0]%username、気をつけてください。\e"
}

//---- システムリソースが低下 -----------------------------------------------------------
OnSysResourceLow
{
	"\1\s[10]システムリソースが少なくなってきたようだ。\0\s[0]作業に不要なプログラムの終了やOSの再起動を提案します。\e"
}


//**** どりふ ***************************************************************************

//---- OnNekodorifObjectHitイベント -----------------------------------------------------
OnNekodorifObjectHit
{
	if reference[4] == "オフィーリア"	// ここにsakura名を指定
	{
		ObjectHitSakura
	}
	else
	{
		ObjectHitKero
	}
}

//---- 本体側に当たった -----------------------------------------------------------------
ObjectHitSakura
{
	if reference[2][0] == "pain" || reference[2][0] == "stab"
	{
		"\0\s[3]痛いです。\e"
	}
	elseif reference[2][0] == "surprise" || reference[2][0] == "huge"
	{
		"\0\s[2]きゃっ！\e"
	}
	elseif reference[2][0] == "love" || reference[2][0] == "elegant" || /
		reference[2][0] == "pretty" || reference[2][0] == "food"
	{
		"\0\s[5]♪\e"
	}
	else
	{
		"\0\s[0]%reference[0]ですか。\e"
	}
}

//---- kero側に当たった -----------------------------------------------------------------
ObjectHitKero
{
	if reference[2][0] == "pain" || reference[2][0] == "stab"
	{
		"\1\s[27]クリティカルーっ\e"
	}
	else
	{
		"\1\s[10]ん。\e"
	}
}
