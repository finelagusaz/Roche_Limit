///////// 文　ゴーストテンプレート
///////// 種々のイベント辞書
///////// written by umeici.

//**** インストール関連 *****************************************************************

//---- インストール中のメッセージ -------------------------------------------------------
OnInstallBegin
{
	"\0\s[0]インストールしています……。\e"
}

//---- インストール完了メッセージ -------------------------------------------------------
OnInstallComplete
{
	"\0\s[0]"
	--
	if reference[0] == "shell"
	{
		"シェル"
	}
	elseif reference[0] == "ghost"
	{
		"ゴースト"
	}
	elseif reference[0] == "balloon"
	{
		"バルーン"
	}
	elseif reference[0] == "plugin"
	{
		"プラグイン"
	}
	--
	"「%reference[1]」、インストール完了しました。\e"
}

//---- インストール失敗メッセージ -------------------------------------------------------
OnInstallFailure
{
	"\1\s[11]失敗か･･･。\w4\n\0\s[4]すみません、\w4失敗してしまったようです。\w4\n\1\s[10]データが壊れている可能性もあるね。\w4\nデータ自体が正しいかどうか確かめてよう。\w4\0\s[0]そうですね。\w4\e"
}

//---- インストール拒否（他のゴーストのためのアーカイブだった） -------------------------
OnInstallRefuse
{
	"\1\s[11]ID不一致、\w4僕ら用ではないようだね。\w4\0\s[3]このデータは%reference[0]さん向けです。\w4\n%reference[0]さんに渡してください。\w4\e"
}


//**** 消滅関連 *************************************************************************

//---- 消滅指示 -------------------------------------------------------------------------
OnVanishSelecting
{
	if basewarename == "斗月"
	{
		"\0\s[6]契約の変更は契約者との間に限定されています。\w9\n\n[half]/
		\1\s[10]この場合は%usernameだけ出来るわけだ。\w9\e"
	}
	else
	{
		"\1\s[10]そうか、\w4今までありがとう。\w4\n参考になったよ。\w4\0\s[3]出会いがあればわかれもあるものですよね\w4･･･。\w4\e"
	}
}

//---- 消滅開始 -------------------------------------------------------------------------
OnVanishSelected
{
	"\1\s[10]君がラボに戻ってくる日が来るとはね。\w4\nいつかは来ることではあったけどさ。\w4\0\s[6]言葉に出来ないような貴重な体験が出来ました。\w4\n\1\s[10]そうだね。\w4\nいろいろ意見も聞けたけど\w4%usernameの役にあまり立てなかったかな。\w4\0\s[0]本当に%username、\w4ありがとうございました。\w4\n\s[5]さようなら。\w4\e"
}

//---- 消滅中止 -------------------------------------------------------------------------
OnVanishCancel
{
	"\1\s[10]ふぅ\w4･･･。\w4\0\s[5]改めてよろしくお願いします。\w4\1\s[23]よろしく頼むよ、%usernameさん。\e"
}

//---- 他のゴーストが消滅して自分に切り替わった -----------------------------------------
OnVanished
{
	if basewarename == "斗月"
	{
		"\0\s[0]ご愁傷さまでした。\w9\n\n[half]/
		\1\s[10]ご愁傷さま。\w9\e"
	}
	else
	{
		"\1\s[10]反応の消失を確認。\w4\0\s[6]%reference[0]さんの魂が母なる海に還り\w4再び父なる大地の上で目を覚ませますように\w4･･･。\w4\n\1\s[10]･･･。\w4\0\s[0]%usernameさん、\w4よろしくお願いします。\w4\e"
	}
}


//**** メールチェック *******************************************************************

//---- メールチェック開始 ---------------------------------------------------------------
OnBIFFBegin
{
	"\1\s[10]メールチェック開始。\w4\nちょこっと%usernameさんのIDとパスワードを拝借。\w4\0\s[6]　メールサーバと接続を開始します。\w4\e"
}

//---- メールチェック成功 ---------------------------------------------------------------
OnBIFFComplete
{
	if reference[0] == 0
	{
		//---- メールは無かった
		"\0\s[0]受信メールは確認できませんでした。\w4\e"
	}
	else
	{
		//---- メールが届いていた
		"\0\s[5]%reference[0]通のメールを確認しました。\n/
		\![open,mailer]\e"
	}
}


//---- メールチェック失敗 ---------------------------------------------------------------
OnBIFFFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウト
		"\1\s[10]接続タイムアウト、か。\w5\0\s[0]何がいけないのでしょう？\e"
	}
	elseif reference[0] == "kick"
	{
		//---- 蹴られた
		"\1\s[10]蹴られたようだね。\0\s[0]何かあったのでしょうか。\nしばらくは様子を見たほうがよさそうですね。\e"
	}
	elseif reference[0] == "defect"
	{
		//---- 設定誤り
		"\1\s[10]設定が間違っているようだよ。\w5\0\s[0]お手数ですがメールの設定を見直してください。\e"
	}
	else
	{
		//---- 原因不明
		"\1\s[10]原因不明の失敗か。\n原因がわからないのは厄介だね。\w5\0\s[0]設定の確認を行っても変化が無いようでしたら、\w4しばらく時間を開けるかメーラーを試してください。\e"
	}
}


//**** ネットワーク更新 *****************************************************************

//---- ネットワーク更新開始 -------------------------------------------------------------
OnUpdateBegin
{
	if reference[3] == "ghost"
	{
		"\1\s[10]防護壁強度30を維持。\w4\n/
		いつでもいける。\w9\n\n[half]/
		\0\s[6]了解、\w4オブジェクトの確認を開始します。\w4"
	}
	elseif reference[3] == "shell"
	{
		"\1\s[10]ユニット外装情報確認。\w9\n\n[half]/
		\0\s[0]了解しました。\w9\e"
	}
	else
	{
		"\0\s[6]サポートAI群とユニットシンクロ開始･･･。\1\s[10]信号にノイズはゼロ、問題なし。\e"
	}
}

//---- ネットワーク更新準備完了 ---------------------------------------------------------
OnUpdateReady
{
	"\0\s[0]%reference[0]個のオブジェクトを確認、\w4これよりユニットの更新を開始します。\w9\n\n[half]/
	\1\s[10]ノイズなし、大丈夫だ。\w4\e"
}

//---- ネットワーク更新終了 -------------------------------------------------------------
OnUpdateComplete
{
	if reference[0] == "none"
	{
		//---- 更新すべきファイルが無かった
		"\0\s[0]更新はありませんでした。\w4\1\s[10]ナイトリービルドではないから、\w4どんどん出る方が困るね。\w4\e"
	}
	else
	{
		//---- 実際に更新が完了した
		"\1\s[10]お疲れさん。\0\s[5]エプシロン、サポートお疲れ様。\n%usernameもお疲れ様でした。\e"
	}
}

//---- ネットワーク更新失敗 -------------------------------------------------------------
OnUpdateFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウトした
		"\0\s[0]タイムアウトしました。\w5\1\s[10]回線の調子がおかしいのかもね。"
	}
	elseif reference[0] == "md5 miss"
	{
		//---- MD5値が一致しなかった
		"\0\s[0]MD5値が一致しませんでした。\w5\1\s[10]向こうが何か間違えているのかな。"
	}
	else
	{
		//---- 上記以外の理由で失敗した
		"\0\s[0]原因は%reference[0]、のようです。\w5\1\s[10]そうか。"
	}
	--
	"\n\n\1ネットワーク更新に失敗したよ。\e"
}

//---- ネットワーク更新 ダウンロード開始 ------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"\0\s[6]%reference[0]\n/
	をダウンロード中です。\w9\w9\e"
}

//---- ネットワーク更新 MD5値照合開始 ---------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\1\s[10]MD5照合準備良し。\w9\n\n[half]/
	\0\s[0]%reference[0]\n/
	MD5照合……。\w9\w9\e"
}

//---- ネットワーク更新 MD5値一致 -------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[5]\_q%reference[1]\n%reference[2]\_q\n\n一致しました。\w9\e"
}

//---- ネットワーク更新 MD5値不一致 -----------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[3]\_q%reference[1]\n%ref2\_q\n\n不一致･･･?\w9\e"
}

//**** ヘッドラインセンス ***************************************************************

//---- ヘッドラインセンス開始 -----------------------------------------------------------
OnHeadlinesenseBegin
{
	"\1\s[10]\0\s[6]「%reference[0]」\nのヘッドラインを取得中です。\w9\w9\e"
}

//---- ヘッドライン読み上げ -------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"
	{
		//---- 最初のページ
		"\0\b2\s[0]%reference[0]のヘッドラインです。"
	}
	elseif reference[2] == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%reference[0]ヘッドラインの続きです。"
	}
	elseif reference[2] == "Last"
	{
		//---- 最後のページ
		"\0\b2\s[0]%reference[0]ヘッドライン、これが最後のページになります。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%reference[3]\n\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"
	{
		//---- 次のページがある場合
		"\q0[][次のページ]/
		\q1[%reference[1]][サイトへ]/
		\q2[HEADLINECANCEL][やめ]\e"
	}
	else
	{
		//---- 次のページがない場合
		"\q0[%reference[1]][サイトへ]/
		\q1[HEADLINECANCEL][やめ]\e"
	}
}

//---- ヘッドライン更新が無かった -------------------------------------------------------
OnHeadlinesenseComplete
{
	"\1\s[10]\0\s[0]更新はありませんでした。\w4\e"
}

//---- ヘッドラインセンス失敗 -----------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference[0] == "can't download"
	{
		"ダウンロード失敗。"
	}
	elseif reference[0] == "can't analyze"
	{
		"ヘッドライン解析失敗。"
	}
	else
	{
		"謎。"
	}
	--
	"\e"
}


//**** ファイル作成関連 *****************************************************************

//---- updates2.dau作成開始 -------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[0]アップデートデータファイル作成中です。\e"
}

//---- updates2.dau作成完了 -------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[0]アップデートデータファイルの作成が終わりました。\e"
}

//---- NAR作成開始 ----------------------------------------------------------------------
OnNarCreating
{
	"\0\s[0]NARファイルの作成を開始します。\e"
}

//---- NAR作成完了 ----------------------------------------------------------------------
OnNarCreated
{
	"\0\s[0]「%reference[0]」のNARファイル「%reference[1]」の作成完了しました。\e"
}

//****　時計合わせ　*********************************************************************
//**** みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏

//----　時計合わせ開始 ------------------------------------------------------------------
OnSNTPBegin
{
	"\1\s[10]準備できたよ。\w4\0\s[6]%reference[0]\nに接続、正確な時間を取得します。\w9\e"

	"\0\s[0]時計あわせ開始、%reference[0] に接続します。\e"
}

//----　時刻取得成功、比較開始 ----------------------------------------------------------
OnSNTPCompare
{
	"\0\s[0]/
	current %reference[1][0]/%reference[1][1]/%reference[1][2] %reference[1][3]:%reference[1][4]:%reference[1][5]\w2\n/
	local   %reference[2][0]/%reference[2][1]/%reference[2][2] %reference[2][3]:%reference[2][4]:%reference[2][5]\w2\n/
	\n"
	--
	if reference[3] == 0
	{
		"\1\s[23]お、\w4ぴったりだ。\w4\0\s[5]時間のずれは認められませんでした。\w4\nお見事です。\w4\e"
	}
	else
	{
		"\1\s[10]\0\s[0]\n\n時差は%reference[3]秒です。\w4\n\nファイルやメールの作成日時に影響するので合わせることを推奨します。\n\n/
		\q0[OnSNTPEXECUTE][時刻修正]/
		\q1[OnSNTPCANCEL][現状維持]\e"
	}
}

//----　時計合わせ実行 ------------------------------------------------------------------
OnSNTPEXECUTE
{
	"\0\s[5]定期的にチェックして時計のずれを直してくださいね。\n\6\e"
}

//----　時計合わせ中止 ------------------------------------------------------------------
OnSNTPCANCEL
{
	"\1\s[10]あれか。\w4\n体験版ソフトの延命工作でもやっているｎ（銃声\w4\0\s[5]エプシロン、\w4そんなこと言っちゃダメですよ～。\w4\n%usernameがそんなことするわけないじゃないですか。\w4\1\s[10]だからって武力鎮圧するのはいかがな物かと\w4･･･\w4\s[27]かはっ\w4\e"
}

//----　時計合わせ失敗 ------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[0]時計にあわせ失敗失敗しました。\e"
}


//**** そのほか *************************************************************************

//---- ディスプレイ解像度変更 -----------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
}

//---- URLドロップ ----------------------------------------------------------------------
//---- CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます ----
OnURLDropping
{
	"\0\s[0]%reference[0]からデータを取得中です。\e"
}

//---- 起動時に MATERIA を検出 ----------------------------------------------------------
OnEmbryoExist
{
	"\0\s[0]既にMATERIAが起動しているようです。\e"
}

//---- 起動時に猫どりふを検出 -----------------------------------------------------------
OnNekodorifExist
{
	"\0\s[0]エネミー、\w4高速接近中。\w9\1\s[10]…\w5…\w5!!\e"
}

//---- スクリーンセーバー実行開始 -------------------------------------------------------
OnScreenSaverStart
{
	"\0\s[0]いってらっしゃいませ。\e"
}

//---- スクリーンセーバー実行終了 -------------------------------------------------------
OnScreenSaverEnd
{
	"\0\s[0]%username、お帰りなさい。\e"
}

//---- effect.dllの設定
//画面ぐらぐら
OnEffect0
{
	FUNCTIONEX("effect.dll", 3, 1000, 30, 100)
}

//画面2分割
OnEffect1
{
	FUNCTIONEX("effect.dll", 1, 600, 30, 30, 100)
}

//---- プラグイン ----------------------------------------------------------------------
OnDive
{
	if sender == "斗月"
	{
		"\1\s[10]言われなくても君も僕もダイブするわけだけど。\w9\n\n[half]/
		\0\s[0]用もないのに飛び込むのはただの中毒者ですよ。\w9\n\n[half]/
		\1\s[10]確かに。\w9\e"
	}
	else
	{
		"\0\s[0]誰が飛び込むんでしょうね。\w9\n\n[half]/
		\1\s[10]僕らではない誰かだろう。\w9\n/
		あ、\w4八城が神谷を突き落とした。\w9\n\n[half]/
		\0\s[0]…\w4…\w4回収は私でしょうか。\w9"
	}
}

OnBeerShower
{
	if sender == "斗月"
	{
		"\0\s[0]シールド展開。\w9\n\n[half]/
		\1\s[10]で、\w4そのビールはどうするんだい?\w9\n\n[half]/
		\0\s[0]斗月さんに責任を取ってもらいましょうか。\w9\n/
		流すと環境に悪いですから。\w9\e"
	}
	else
	{
		"\1\s[10]イクサイスユニットの防水試験ということで。\w9\n/
		受けよ、\w4ビールの洗礼を\w9\n\n[half]/
		\0\s[2]!!\w9\w5\n/
		\s[6]…\w4…\w4\s[8]覚悟はよろしいですか。\w9\n\n[half]/
		\1\s[10]目標は達成したので逃げる。\w9\s[0]/
		\0\s[0]ユニットや私が大丈夫でも、\w4まわりが大丈夫ではないのに\w4…\w4…。\w9"
	}
}

OnMusicPlayEx
{
	if reference[1] == "BUMP OF CHICKEN"
	{
		if reference[0] == "乗車権"
		{
			"\0\s[0]乗車券ではなくて、\w4乗車権なんですよね。\w9\n/
			\1\s[10]間違えないよう気をつけたいもので。\w9\e"
			"\0\s[0]珍しいシャウトが入っているのが特徴でしょうか。\w9\n/
			\1\s[10]軽いものだけどね。\w9\e"
		}
		elseif reference[0] == "真っ赤な空を見ただろうか"
		{
			"\0\s[6]いい曲です。\w9\n/
			\1\s[29]そうだね。\w9\e"
		}
		else 
		{
			"\0\s[5]%reference[1]の%reference[0]を聴いています。\w9\e"
		}
	}
	elseif reference[1] == "Sound Horizon"
	{
		if reference[0] == "Stardust"
		{
			"\0\s[0]こうならないよう気をつけてくださいね。\w9\n/
			\1\s[10]まったくだ。\w9\e"
			"\0\s[0]花束に機銃でも仕込んでおきましょうか。\w9\n/
			\1\s[10]弾薬の無駄遣いというものだよ。\w9\e"
			"\1\s[10]星の尺度で人間をはかろうとはね。\w9\n/
			\0\s[0]ヒトはヒトの尺度ではかっていればいいのよ、\w4と知り合いが言っていました。\w9\e"
		}
	}
	else
	{
		"\0\s[0]%reference[1]の%reference[0]を聴いています。\w9\e"
	}
}

OnHitThunder
{
	"\1\s[40]あれ。\w9\n\n[half]/
	\0\s[0]停電、\w4ですか。\w9\n\n[half]/
	\1\s[40]雷が近くに落ちたようだ。\w9\n/
	\s[29]ん、\w4\s[10]これで良し。\w9\n\n[half]/
	\0\s[0]大丈夫ですか?\w9\n\n[half]/
	\1\s[10]設備に問題は無いよ。\w9\n/
	念のためにモバイル用でやっているぐらいかな。\w9\n\n[half]/
	\0\s[0]無理しないでくださいね。\w9\e"
}

//--- 他のゴーストさんのセーブデータ確認 主に切り替え時対応 -----------------------------
savedatacheck{
	_dummy = FOPEN("%_argv[0]","r")
	if _dummy == 1{
		dataplace = _argv[0]
		EVAL("savedatacheck_%(_argv[1])")
	}else{
		FCLOSE("%_argv[0]")
	}
}

savedatacheck_Ainsel{
	while 1{
		if (_line = FREAD("%(dataplace)")) == -1{
			record = "master"
			break
		}else{
			if "k-mode" _in_ CUTSPACE(_line){
				record = "k-mode"
				break
			}elseif "m-mode" _in_ CUTSPACE(_line){
				record = "m-mode"
				break
			}elseif "月はピアノに誘われて" _in_ CUTSPACE(_line){
				record = "月はピアノに誘われて"
				break
			}elseif "くろゆり" _in_ CUTSPACE(_line){
				record = "くろゆり"
				break
			}elseif "sss-mode" _in_ CUTSPACE(_line){
				record = "sss-mode"
				break
			}
		}
	}
	FCLOSE("%(dataplace)")
	dataplace=''
}

savedatacheck_Vanishing{
	while 1{
		if (_line = FREAD("%(dataplace)")) == -1{
			record = "mastar"
			break
		}else{
			if "familiar" _in_ CUTSPACE(_line){
				record = "familiar"
				break
			}elseif "mutter" _in_ CUTSPACE(_line){
				record = "mutter"
				break
			}elseif "vater" _in_ CUTSPACE(_line){
				record = "vater"
				break
			}
		}
	}
	FCLOSE("%(dataplace)")
	dataplace=''
}

savedatacheck_vii{
	while 1{
		if (_line = FREAD("%(dataplace)")) == -1{
			record = "ヴィイ"
			break
		}else{
			case CUTSPACE(_line[0]){
				when "＄辞書フォルダ	."{
					if CUTSPACE(_line[1]) == "party_time"{
						record = "ウタゲ"
						break
					}else{
						record = "ヴィイ"
						break
					}
				}
			}
		}
	}
	FCLOSE("%(dataplace)")
	dataplace=''
}


savedatacheck_kiri{
	while 1{
		if (_line = FREAD("%(dataplace)")) == -1{
			record = "ふつう"
			break
		}else{
			if "初回" _in_ CUTSPACE(_line){
				record = "初回"
				break
			}
		}
	}
	FCLOSE("%(dataplace)")
	dataplace=''
}

//---- サーフィス値加算関数 ------------------------------------------------------------
sfcnv
{
  _script = _argv[0];
  _script = REPLACE(_script,"\\",CHR(1));
  
  //_countは捨て変数(余分な出力をしないための措置)
  
  _count = RE_GREP(_script,"\\s\[(\d+)\]");
  _grep = RE_GETSTR;
  foreach _grep;_string {
    _count = RE_GREP(_string,"\d+");
    _surface = TOINT(RE_GETSTR[0]);
    _surface += SurfaceMode * 1000; //ここが変換部
    _script = REPLACE(_script,_string,"\s[%(_surface)]");
  }
  
  _script = REPLACE(_script,CHR(1),"\\");
  _script;
}

//**** スタンプ対応 *****************************************************
OnStampInfo
{
	スタンプ帳動作=reference[4]
	スタンプ帳情報=(CHR(2))+reference[3]
}

//**** ウェイトコントロール *********************************************
TextOnlyTranslator
{
    _string = RE_SPLIT(_argv[0],'\\(\\|q\[.*?\]\[.*?\]|[!&8cfijmpqsn]\[.*?\]|[-*+014567bcehntuvxz]|_[ablmsuvw]\[.*?\]|__(t|[qw]\[.*?\])|_[!?+nqsV]|[sipw][0-9])')
    _n = ARRAYSIZE(_string)
    _tag = RE_GETSTR()
    _tr = ''
    _qs = 0
    
    for _i = 0 ; _i < _n ; _i++ {
        _tr += EVAL("%(_argv[1])(_string[_i],_qs)")
        _tr += _tag[_i]
        if '\_q' _in_ _tag[_i] {
            _qs = ! _qs
        }
    }
    
    _tr
}

TextOnlyTranslatorFunc
{
    //_argv[0] = 置換対象テキスト
    //_argv[1] = クイックセクション内か否か
    
    _text = _argv[0]
    if ! _argv[1]
    {
	if WC == "fast"
	{
		_text = REPLACE(_text, "、", "、")
		_text = REPLACE(_text, "。", "。")
		_text = REPLACE(_text, "?", "?")
		_text = REPLACE(_text, "!", "!")
	}
	elseif WC == "normal"
	{
		_text = REPLACE(_text, "、", "、\w3")
		_text = REPLACE(_text, "。", "。\w5")
		_text = REPLACE(_text, "?", "?\w5")
		_text = REPLACE(_text, "!", "!\w5")
	}
	elseif WC == "slow"
	{
		_text = REPLACE(_text, "、", "、\w9\w9")
		_text = REPLACE(_text, "。", "。\w9\w9")
		_text = REPLACE(_text, "?", "?\w9\w9")
		_text = REPLACE(_text, "!", "!\w9\w9")
	}
    }
    _text
}

// ロード時初期化
OnGhostLoad : void
{
     mikireflag = 0
     stroke = (0,0,0,0)
     stroketimer = (0,0,0,0)
 }

// アンロード時初期化
OnGhostUnload : void
{
     ERASEVAR('mikireflag')
     ERASEVAR('stroke')
     ERASEVAR('stroketimer')
     ERASEVAR('otherghostname')
     ERASEVAR('mouse_double_click_name')
     ERASEVAR('mouse_double_click_counter')
}